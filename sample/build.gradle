plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def properties = new Properties()
properties.load(new FileInputStream(rootProject.file('local.properties')))

def callTopic = properties.getProperty('call.topic') ?: '"Some topic"'

def widgetTitles = properties.getProperty('widget.titles') ?: "Some name"
def widgetLinks = properties.getProperty('widget.links') ?: "Some name"

android {
    namespace 'kz.qbox.widget.webview.sample'

    compileSdk 33

    defaultConfig {
        applicationId 'kz.qbox.widget.webview'

        minSdk 21
        targetSdk 33

        versionCode project.ext.version_code
        versionName version
    }

    buildTypes {
        debug {

            buildConfigField 'String', 'CALL_TOPIC', callTopic
            buildConfigField "String[]", "WIDGET_TITLES", "{${widgetTitles.split(',').collect { '\"' + it + '\"' }.join(',')}}"
            buildConfigField "String[]", "WIDGET_LINKS", "{${widgetLinks.split(',').collect { '\"' + it + '\"' }.join(',')}}"

            minifyEnabled false

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

dependencies {
    // AppCompat
    implementation 'androidx.appcompat:appcompat:1.5.1'

    // Core
    implementation 'androidx.core:core-ktx:1.9.0'

    // Material Design Components
    implementation 'com.google.android.material:material:1.7.0'

    // Garage SDK
    implementation 'com.github.zhombie.garage:image-coil:1.3.5'

    implementation project(':core')
}